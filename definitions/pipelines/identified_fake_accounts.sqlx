config {
  type: "incremental",
  columns: {
    row_num: "This is a random row_num assigned between 1 to 1000000",
    name: "Person's name",
    phone: "Person's phone number",
    ipv6: "Last known IPv6 address",
    comp_name: "Company name",
    ingestion_date: "Timestamp at which this record was ingested"
  }
}

pre_operations {
  declare last_refresh default (
    ${when(incremental(),
    `select max(ingestion_date) from ${self()}`,
    `select timestamp("2000-01-01")`)}
  )
}

SELECT
  r.*
FROM 
  ${ref("report_table")} r, 
  ${ref("fake_accounts")} f
WHERE
  r.name = f.name
  AND r.phone = f.phone

${when(incremental(), `AND r.ingestion_date > last_refresh`)}

