-- Copyright 2022 Google. This software is provided as-is, without warranty or representation for any use or purpose. Your use of it is subject to your agreement with Google.  

config {
  type: "incremental",
  //dependencies: [ "append_date_to_source_table" ],
  bigquery: {
    partitionBy: "TIMESTAMP_TRUNC(ingestion_date, HOUR)",
    clusterBy: ["comp_name", "name"]
  },
  assertions: {
    nonNull: ["name", "phone"],
    uniqueKey: ["row_num"],
    rowConditions: [
      "ingestion_date > '2020-05-01'",
      "REGEXP_CONTAINS(phone, r'^\\d-\\d{3}-\\d{3}-\\d{3}$')"
    ]
  },
  columns: {
    row_num: "This is a random row_num assigned between 1 to 1000000",
    name: "Person's name",
    phone: "Person's phone number",
    ipv6: "Last known IPv6 address",
    comp_name: "Company name",
    ingestion_date: "Timestamp at which this record was ingested"
  }
}

pre_operations {
  declare last_refresh default (
    ${when(incremental(),
    `select max(ingestion_date) from ${self()}`,
    `select timestamp("2000-01-01")`)}
  )
}

SELECT
  row_num as row_num,
  name as name,
  phone as phone,
  ipv6 as ipv6,
  comp_name as comp_name,
  ingestion_date as ingestion_date,
  new_column as new_column
FROM ${ref("source_table_view")}

${when(incremental(), `WHERE ingestion_date > last_refresh`)}

